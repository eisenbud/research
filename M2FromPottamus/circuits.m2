--program to find circuits by Groebner methods.
--Prop: if A:V=<e1..en> --> K^l represents a hyperplane
--arrangement, and I is the ideal in \wedge V generated by
--the kernel, then the circuits are just the monomials in I.
--Thus it suffices to apply the old idea of finding the
--monomial part of an ideal by repeatedly finding the homogeneous part.

restart
--make a random "toric" --- that is, diagonal,
-- change of variables in a ring E
randomTorus = E->(
rT :=matrix{apply(
	  (rank source vars E),i-> ((random char E)*(vars E)_(0,i))
	  )};
     map(E,E,rT))

--finds the largest monomial ideal
--contained in the ideal I in
--the exterior algebra.


emonomials = (I,t)->(
     E:=ring I;
     J:=gb I;
     gJ:=gens J;
     mgJ:=mingens J;
     while 
       rank source compress ((mgJ)-(leadTerm mgJ)) =!=0  
       do (
     L:=flatten(
       	  ideal leadTerm gJ, 
          ideal mgJ,
          apply(t,i->((randomTorus(E)) (ideal mgJ)))
              );
     J= gb intersect(L);
     gJ= gens J;
     mgJ= mingens J);
     --   print betti compress ((mingens gJ)-(leadTerm mingens gJ))
      ideal mingens J)


-- finds the scalar
-- relations on a 
-- list of n linear forms, given as entries of a 1xn matrix
-- J. Takes as second argument a polynomial ring S with
-- n generators of degree 1, and returns the relations as
-- linear forms in S.

scalarRelations = (J,R) -> (
     S  := ring J;
     F  := syz(J, DegreeLimit =>1);
     RS := map(R,S,matrix{toList(numgens S:0_R)});
     (vars R)*(RS F)
     )


--given a matrix of n linear forms J and an exterior algebra
--E on n generators (of the same characteristic), 
--finds the monomials in E corresponding
--to the circuits of J (circuit = minimal dependent subset)
circuits = (J,E)->(
     R  := ZZ/(char ring J)[vars(1..(rank source J))];
     ER := map(E,R,vars E);
     emonomials ER ideal scalarRelations(J,R))

--test
kk=ZZ/32003
S=kk[a,b,c]
J=matrix{{a,b,c,a-b,a-c,b-c}}
R=kk[x_1..x_6]
scalarRelations(J,R)

E=kk[x_1..x_(rank source J),SkewCommutative=>true]
ER=map(E,R,vars E)
IE=ideal ER scalarRelations(J,R)

time emonomials6(IE,4)
time circuits(J,E)
--almost all the time is spent in emonomials

--ideal generated by d general linear forms in n vars
example = (n,d,t) ->(
     E=kk[x_1..x_n,SkewCommutative=>true];
     I=ideal random(E^1,E^{d:-1});
     time emonomials6(I,t)
                    )
example(7,6,1)





